#import "Lib_v0+equal+know";
#define TimeLock 5;
#define N 4;
#define Notification1 10;
#define Notification2 11;
#define Notification3 12;
#define Go 1;
#define Off 0;

channel SenderAndRecipient 0;
channel SenderAndSourceChain 0;
channel RecipientAndSourcechain 0;
channel SourceChainAndSourceMiner 0;
channel DestinationAndSourceMiner 0;
channel SourceAndDestination 0;
channel DestinationAndDestinationMiner 0;
channel SourceAndDestinationMiner 0;
channel SenderAndDestination 0;
channel RecipientAndDestination 0;
channel SourceMinerAndEvent 0;
channel DestinationMinerAndEvent 0;
channel SenderAndFakeRecipient 0;


channel SourceChainAndEvent 0;
channel DestinationAndEvent 0;

channel Miner1AndSourceMiner 0;
channel Miner2AndSourceMiner 0;
channel Miner1AndDestinationMiner 0;
channel Miner2AndDestinationMiner 0;

var<SKey> SKsender = new SKey();
var<PKey> PKsender = new PKey(SKsender); 
var<SKey> SKreceive = new SKey();
var<PKey> PKreceive = new PKey(SKreceive); 
var<Nonce> Message = new Nonce();

var moneyS = 100;
var moneySnew;
var moneyR = 0;
var moneyRnew ;
var asset = 10;
var BurnAddress = 0;
var FakeReceiverMoney = 0;
var moneyS_NoEnought = false;
var fakerecipientHasNoTruePk = true;
var truereceiverHasTruePK = false;


var AddressR = [PKreceive, SKreceive, moneyS];
var AddressS = [PKsender, SKsender, moneyR];

var event1 = [PKsender, Message, asset];
var event2 = [PKreceive, Message, asset];
var event3 = 0;

var moneySnewCheck = false;
var reclaimResult = false;
var MinerResultVerification1 = false;
var MinerResultVerification2 = false;

var HashResult;
var<Nonce> Math_problemS = new Nonce();
var<Nonce> Math_problemD = new Nonce();
var<Sign> digital_signature;
var<AEnc> encrypted_inf;
var<ADec> decrypted_inf;
var<Sign> decrypted_Sign;

var n = 0;
var NewBlockS = [0,0,0,0,0,0,0,0,0,0,0,0];
var NewBlockD = [0,0,0,0,0,0,0,0,0,0,0,0];
// digital_signature = Digital_Signature{new Pair(PKreceive,Message)} -> Skip;



sender() = SenderAndRecipient!PKsender -> 
SenderAndRecipient?PKreceive1 -> 
SenderAndRecipient?BurnAddress1 -> 
Digital_Signature{digital_signature = new Sign(Message,SKsender);} -> 
Encrypted_inf{encrypted_inf = new AEnc(digital_signature, PKreceive);} -> 
SenderAndSourceChain!encrypted_inf.asset.BurnAddress1.digital_signature.PKsender -> Skip();



SourceChain() = SenderAndSourceChain?encrypted_inf1.asset1.BurnAddress2.digital_signature1.PKsender1 -> 
ExitTransaction{if(digital_signature1==digital_signature && PKsender1==PKsender && moneyS >= asset1)    //ExitTransaction
                  {moneySnew = moneyS - asset1;}
                  else{moneyS_NoEnought = true;}} ->
Burn_Transaction{if(moneySnew < moneyS && moneySnew > 0)     //Burn_Transaction
                    {BurnAddress = BurnAddress2 + asset1;
                     moneyS = moneySnew;}} ->
SourceChainAndSourceMiner!moneySnew.BurnAddress.encrypted_inf1.digital_signature1.asset1 ->
SourceChainAndSourceMiner?MinerResultVerification1new ->
SourceChainAndEvent!event1 ->
SourceAndDestination!moneySnew.BurnAddress.encrypted_inf1.digital_signature1.asset1.MinerResultVerification1new  ->  Skip();



DestinationChain() = SourceAndDestination?moneySnew1.BurnAddress1.encrypted_inf2.digital_signature2.asset2.MinerResultVerification1new1 ->
EntryTransaction{if(moneySnew1<=moneyS && BurnAddress1==asset2 && encrypted_inf2 == encrypted_inf && digital_signature2 == digital_signature && MinerResultVerification1new1 == true)  // EntryTransaction
                 {decrypted_inf = new ADec(encrypted_inf2,SKreceive);
                  decrypted_Sign = decrypted_inf.result();}} ->
AssetCreate{if(decrypted_Sign.Signequal(digital_signature2) == true)  // AssetCreate digital_signature2
               {moneyRnew = moneyR  + asset2;
                truereceiverHasTruePK = true;}} ->
DestinationAndDestinationMiner!decrypted_inf.encrypted_inf2 ->
DestinationAndDestinationMiner?MinerResultVerification2new ->
DestinationAndEvent!event2 ->
RecipientAndDestination!moneyRnew.MinerResultVerification2new -> Skip();



recipient() = SenderAndRecipient?PKsender1 -> 
SenderAndRecipient!PKreceive -> 
SenderAndRecipient!BurnAddress -> 
RecipientAndDestination?moneyRnew1.MinerResultVerification2new1 ->
MoneyGet{if(MinerResultVerification2new1 == true){moneyR = moneyRnew1;}} -> Skip();



SourceMiner() = SourceChainAndSourceMiner?moneySnew11.BurnAddress11.encrypted_inf111.digital_signature111.asset111 -> 
ResultVerification{if(moneySnew11<=moneyS && BurnAddress11==asset){MinerResultVerification1 = true;}} -> 
HashCalculation{HashResult = new Hash(Math_problemS);} -> 
Miner1AndSourceMiner!HashResult ->
Miner2AndSourceMiner!HashResult ->
Miner1AndSourceMiner?NewBlockS1 ->
Miner2AndSourceMiner?NewBlockS2 ->
SourceChainAndSourceMiner!MinerResultVerification1 -> Skip();



DestinationMiner() = DestinationAndDestinationMiner?decrypted_inf1.encrypted_inf21 ->
ResultVerification{if(decrypted_inf1 == digital_signature){MinerResultVerification2 = true;}} ->
HashCalculation{HashResult = new Hash(Math_problemD);} ->
Miner1AndDestinationMiner!HashResult ->
Miner2AndDestinationMiner!HashResult ->
Miner1AndDestinationMiner?NewBlockS1 ->
Miner2AndDestinationMiner?NewBlockS2 ->
DestinationAndDestinationMiner!MinerResultVerification2 -> Skip();



Miner1() = Miner1AndSourceMiner?HashResult1 -> 
           AddNewBlock{NewBlockS[n] = 1; n++;} -> 
           Miner1AndSourceMiner!NewBlockS ->
           Miner1AndDestinationMiner?HashResult11 -> 
           AddNewBlock{NewBlockS[n] = 1; n++;} -> 
           Miner1AndDestinationMiner!NewBlockS -> Skip;
           
           
           
Miner2() = Miner2AndSourceMiner?HashResult2 -> 
           AddNewBlock{NewBlockS[n] = 1; n++;} -> 
           Miner2AndSourceMiner!NewBlockS ->
           Miner2AndDestinationMiner?HashResult22 -> 
           AddNewBlock{NewBlockS[n] = 1; n++;} -> 
           Miner2AndDestinationMiner!NewBlockS -> Skip;
           
           
           
Event() = SourceChainAndEvent?event1new -> DestinationAndEvent?event2new -> Skip();



protocol() = sender() || recipient() || SourceChain() || DestinationChain() || SourceMiner() || Event() || DestinationMiner() || Miner1() || Miner2();



# assert protocol() deadlockfree;               // dead-lock free


# define BurnAddressMoney (BurnAddress == 10);  // unspendability
# assert protocol() |= <>[] BurnAddressMoney;





# define BurnTrans (BurnAddress == 10);        // burn before claim
# define MoneyCreate (moneyRnew == 10);
# assert protocol() |= [] (([] !MoneyCreate) || (!MoneyCreate U BurnTrans));







# define MoneyS_NoEnought (moneyS_NoEnought == true);   // Ownership
# assert protocol() |= <>[] MoneyS_NoEnought;

# define money_S (moneyS == 100);                           
# assert protocol() |= <>[] money_S;

# define money_R (moneyR == 0);                
# assert protocol() |= <>[] money_R;







# define FakerecipientHasNoTruePk (fakerecipientHasNoTruePk == true);      //Security
# assert protocol() |= <>[] FakerecipientHasNoTruePk;

# define TrueReceiverHasTruePK (truereceiverHasTruePK == true);
# assert protocol() |= <>[] TrueReceiverHasTruePK;






# define money_S (moneyS == 90);               // accuracy             
# assert protocol() |= <>[] money_S;

# define money_R (moneyR == 10);                
# assert protocol() |= <>[] money_R;

# define FakeReceiver_Money (FakeReceiverMoney == 0);                
# assert protocol() |= <>[] FakeReceiver_Money;
protocol() = sender() || recipient() || SourceChain() || DestinationChain() || SourceMiner() || Event() || DestinationMiner() || Miner1() || Miner2();
